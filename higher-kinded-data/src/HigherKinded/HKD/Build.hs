{-# LANGUAGE BlockArguments #-}
{-# LANGUAGE DataKinds #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE FunctionalDependencies #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE PatternSynonyms #-}
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE TypeOperators #-}
{-# LANGUAGE UndecidableInstances #-}
{-# LANGUAGE ViewPatterns #-}

-- | Based on 'Data.Generic.HKD.Named' from package 'higgledy'
--   by Tom Harding ((c) Tom Harding, 2019, MIT)

module HigherKinded.HKD.Build
  ( Build (..)
  , pattern (:!)
  , module Named
  ) where

import Data.Generics.Product.Internal.Subtype (GUpcast (..))
import Data.Kind
import GHC.Generics
import Named.Internal as Named (NamedF(..), Name(..), (:!), (:?), pattern Arg)

import HigherKinded.HKD.Generic



class Build (hkd :: Type) (k :: Type) | hkd -> k, k -> hkd where
  build :: k



instance
    ( BuildHKD_ structure hkt f k
    , list ~ Rearrange (HKD_ structure hkt f)
    )
  =>
    Build (HKD structure hkt f) k
  where
    {-# INLINABLE build #-}
    build = gbuild @_ @structure @hkt @f (to . gupcast @list @(HKD_ structure hkt f))

class
    ( GenericHKD_ structure hkt f
    --
    , GUpcast (Rearrange (HKD_ structure hkt f)) (HKD_ structure hkt f)
    , GBuild (Rearrange (HKD_ structure hkt f)) structure hkt f k
    )
  => BuildHKD_ structure hkt f k

instance
    ( GenericHKD_ structure hkt f
    --
    , GUpcast (Rearrange (HKD_ structure hkt f)) (HKD_ structure hkt f)
    , GBuild (Rearrange (HKD_ structure hkt f)) structure hkt f k
    )
  => BuildHKD_ structure hkt f k



class GBuild (rep :: Type -> Type) (structure :: Type) (hkt :: ((Type -> Type) -> Type -> Type)) (f :: Type -> Type) (k :: Type)
    | rep structure hkt f -> k, k -> structure hkt f where
  gbuild :: (forall p. rep p -> HKD structure hkt f) -> k

instance
    GBuild inner structure hkt f k
  =>
    GBuild (D1 meta inner) structure hkt f k
  where
    {-# INLINABLE gbuild #-}
    gbuild rebuild = gbuild (rebuild . M1)

instance
    GBuild inner structure hkt f k
  =>
    GBuild (C1 meta inner) structure hkt f k
  where
    {-# INLINABLE gbuild #-}
    gbuild rebuild = gbuild (rebuild . M1)

instance
    ( rec0 ~ (Rec0 inner)
    , k ~ (name :! inner -> HKD structure hkt f)
    , meta ~ 'MetaSel ('Just name) i d c
    )
  =>
    GBuild (S1 meta rec0) structure hkt f k
  where
    {-# INLINABLE gbuild #-}
    gbuild fill (Arg inner) = fill (M1 (K1 inner))

instance
    ( GBuild right structure hkt f k'
    , rec0 ~ Rec0 x
    , left ~ S1 ('MetaSel ('Just name) i d c) rec0
    , k ~ (name :! x -> k')
    )
  =>
    GBuild (left :*: right) structure hkt f k
  where
    {-# INLINABLE gbuild #-}
    gbuild fill (Arg left) = gbuild \right -> fill (M1 (K1 left) :*: right)



{-# COMPLETE (:!) #-}
pattern (:!) :: Name name -> a -> name :! a
pattern (:!) name a <- (\case { Arg a -> (Name, a) } -> (name, a)) where
  (:!) _ a = Arg a



type family Rearrange (i :: Type -> Type) :: Type -> Type where
  Rearrange (S1       m inner) = S1       m (Rearrange inner)
  Rearrange (M1 index m inner) = M1 index m (Rearrange inner)
  Rearrange (left :*: right)   = Append (Rearrange left) (Rearrange right)
  Rearrange (Rec0 inner)       = Rec0 inner

type family Append (xs :: Type -> Type) (ys :: Type -> Type) :: Type -> Type where
  Append (S1 meta head) tail    = S1 meta head :*: tail
  Append (left :*: right) other = left :*: Append right other

